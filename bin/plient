#!/usr/bin/env perl
use strict;
use warnings;
use Getopt::Long;
require Plient unless $ENV{PLIENT_BUNDLE_MODE};
Plient->import( 'plient', 'plient_support' );

my %args;

GetOptions( \%args, 'help|h', 'support|s=s', 'request|X=s', 'output|o=s' )
  or die 'unknown option';

my $USAGE =<<EOF;
USAGE: plient URL [ ... ]
EXAMPLES:
    plient --support http_get                       # check is support HTTP GET
    plient -s http_get                              # ditto
    plient http://cpan.org/                         # fetch http://cpan.org
    plient -o /tmp/cpan.html http://cpan.org/       # write to file
EOF

if ( $args{help} ) {
    print $USAGE;
    exit;
}

if ( $args{support} ) {
    my ( $protocol, $method ) = split /_/, $args{support}, 2;
    print plient_support( $protocol, $method ) ? 'yes' : 'no';
    print "\n";
    exit;
}

if ( !$ARGV[0] ) {
    print $USAGE;
    exit;
}

my ( @uri ) = @ARGV;
my $method = $args{'request'} || 'get';

for my $uri (@uri) {
    $uri = 'http://' . $uri unless $uri =~ /^\w+:/;
    if ( $args{output} ) {
        plient( $method, $uri, { output_file => $args{output} } );
    }
    else {
        print plient( $method, $uri );
    }
}

